#+title: Ja0nz Emacs Configuration
#+STARTUP: overview
#+PROPERTY: header-args:emacs-lisp :tangle ~/.doom.d/config.el :mkdirp yes

* File Headers
#+begin_src emacs-lisp
;;; $DOOMDIR/config.el -*- lexical-binding: t; -*-
#+end_src

#+begin_src emacs-lisp :tangle ~/.doom.d/packages.el
;; -*- no-byte-compile: t; -*-
;;; $DOOMDIR/packages.el
#+end_src

* Base settings
Here are some additional functions/macros that could help you configure Doom:
- `load!' for loading external *.el files relative to this one
- `use-package!' for configuring packages
- `after!' for running code after a package has loaded
- `add-load-path!' for adding directories to the `load-path', relative to this file. Emacs searches the `load-path' when you load packages with `require' or `use-package'.
- `map!' for binding new keys

** Prelude
#+begin_src emacs-lisp
;; Some functionality uses this to identify you, e.g. GPG configuration, email clients, file templates and snippets.
(setq user-full-name "Ja0nz"
      user-mail-address "mail@ja.nz")

;; Use aa instead of jk - Neo2 Keyboard layout
(use-package! evil-escape
  :init (setq evil-escape-key-sequence "aa"))

;; Use primary selection instead of clipboard
(setq select-enable-primary t)

;; No evil snipe mode if there is avy!
(remove-hook 'doom-first-input-hook #'evil-snipe-mode)
(map! :n "s" #'evil-avy-goto-char-timer)
(map! :n "S" #'avy-goto-char)
(setq avy-all-windows t)
;;(define-key evil-normal-state-map (kbd "s") 'evil-avy-goto-char-timer)
;;(define-key evil-normal-state-map (kbd "S") 'avy-goto-char)

;; No custom file to prevent emacs from buffering agenda files
(setq custom-file "/dev/null")

;; Antagonist for evil-goto-definition -> g d; jump back with -> g b
(map! :map evil-motion-state-map
      :prefix "g"
      "b" #'evil-jump-backward
      "B" #'avy-pop-mark)

;; Fast left/right window switcher
(map! :nie "M-n" #'windmove-left)
(map! :nie "M-t" #'windmove-right)
(map! :nie "M-SPC" #'ace-window)

;; Set chromium as browser
;; Normally the default browser is fine, but with brave/chromium on the same machine things getting tricky
;;(setq browse-url-browser-function 'browse-url-chromium)

;; Smother scrolling
(setq pixel-scroll-precision-mode t)
#+end_src

** Theming, Fonts
#+begin_src emacs-lisp
;; There are two ways to load a theme. Both assume the theme is installed and available. You can either set `doom-theme' or manually load a theme with the `load-theme' function. This is the default:
(setq doom-theme 'doom-dracula)

;; This determines the style of line numbers in effect. If set to `nil', line numbers are disabled. For relative line numbers, set this to `relative'.
(setq display-line-numbers-type t)

;; Fonts settings
(setq doom-font (font-spec :family "JetBrainsMono Nerd Font" :size 15)
      doom-variable-pitch-font (font-spec :family "Arimo Nerd Font" :size 15)
      doom-big-font (font-spec :family "JetBrainsMono Nerd Font" :size 24))
(after! doom-themes
  (setq doom-themes-enable-bold t
        doom-themes-enable-italic t))
(custom-set-faces!
  '(font-lock-comment-face :slant italic)
  '(font-lock-keyword-face :slant italic))
#+end_src
* Custom set variables
Just a dump for some used (project) variables
#+begin_src emacs-lisp
(custom-set-variables
 '(safe-local-variable-values
   '(
      ;; import org agenda files on demand
      (eval . (org-agenda-file-to-front))
      ;; set buffer local
      (ispell-local-dictionary . #'stringp)
      ;; bow ox-hugo to work with non hugo environments
      ;; https://github.com/kaushalmodi/ox-hugo
      (org-hugo-external-file-extensions-allowed-for-copying . nil)
      ;; @include is a SASS feature
      (lsp-css-lint-unknown-at-rules . "ignore")
      ;; exports backend HACK
      (org-export-current-backend . t)
     )))
#+end_src
* Yas Snippets
#+begin_src emacs-lisp
(setq +snippets-dir "~/sysconf/_home-manager/emacs/snippets")
#+end_src
* LSP
#+begin_src emacs-lisp
;; Ignoring output (spago compilation target)
(after! lsp-mode
  (add-to-list 'lsp-file-watch-ignored-directories "[/\\\\]\\output\\'")
  (setq lsp-css-lint-unknown-at-rules "ignore")
)
#+end_src
* Consult
#+begin_src emacs-lisp
(map! :leader
      :desc "Quick Buffer/File Switcher" "SPC" #'consult-buffer)

(map! :leader
      :prefix "s"
      :desc "Consult Org Goto Heading" "g" #'consult-org-heading
      :desc "Consult Org Agenda" "a" #'consult-org-agenda
      :desc "Consult Ripgrep" "p" #'consult-ripgrep)
#+end_src
* Org
** Base Settings
#+begin_src emacs-lisp
(setq org-directory "~/Dropbox/org/"
      _tagsorg (concat org-directory "_tags.org")
      _stageorg (concat org-directory "_stage.org")
      _archiveorg (concat org-directory "_archive.org")
      _habitsorg (concat org-directory "_habits.org"))

;; Org Mode - Base Settings
(setq org-global-properties '(("Effort_ALL" . "0:05 0:10 0:25 0:50 1:15 1:40 2:05 2:55 3:45 4:35 5:25 6:15 7:05"))
      org-agenda-files
      (append
       (list _tagsorg _stageorg _habitsorg)
       (directory-files org-directory t (format-time-string "^W%V")))
      org-agenda-bulk-custom-functions
      '((?m (lambda () (call-interactively 'org-agenda-date-later-minutes)))
        (?h (lambda () (call-interactively 'org-agenda-date-later-hours))))
      org-id-link-to-org-use-id 'create-if-interactive
      org-complete-tags-always-offer-all-agenda-tags t
      org-agenda-start-with-clockreport-mode t
      org-agenda-clockreport-parameter-plist '(:link t :properties ("ALLTAGS" "Effort") :fileskip0 t :compact t)
      org-support-shift-select 'always
      org-goto-interface 'outline-path-completion
      org-startup-with-inline-images t)

;; Fancy priorities
(after! org-fancy-priorities
  (setq
   org-fancy-priorities-list '("ðŸ…°" "ðŸ…±" "ðŸ…¾")))
#+end_src
** Org agenda
#+begin_src emacs-lisp
;; Custom org agenda shortcut
(defun org-agenda-week (&optional arg)
  (interactive "P")
  (org-agenda arg "a"))
;;   (when (= (length (window-list)) 1)
;;     (if (= 0 (car (window-edges)))
;;         (split-window-right)
;;         (split-window-left)))
;;   (let ((org-agenda-window-setup 'other-window)
;;         (current (buffer-name)))
;;     (org-agenda arg "a")
;;     (switch-to-buffer-other-window current)))

(map! :leader :prefix "o" :map global-map :desc "Open Org Agenda Week" "w" #'org-agenda-week)
#+end_src
*** Org agenda icons
#+begin_src emacs-lisp
(cl-defstruct agendaIcons
  category
  icon)

(defvar agenda-icons/caticons)

(setq agenda-icons/caticons
  (cl-flet ((icon #'make-agendaIcons))
    (list
     ;; Leisure
     (icon :category "lang" :icon "language")
     ;; ON -> netflix, hackernews, zeit.de (non work related)
     (icon :category "ON" :icon "toggle-on")
     ;; OFF -> jog, yoga, kite, bike, hike
     (icon :category "OFF" :icon "toggle-off")
     ;; Work
     (icon :category "feat" :icon "code") ;; a new feature
     (icon :category "fix" :icon "bug") ;; A bug fix
     (icon :category "docs" :icon "book") ;; documentation
     (icon :category "style" :icon "eye") ;; formatting
     (icon :category "refactor" :icon "wrench") ;; nor feat || bug
     (icon :category "perf" :icon "tachometer") ;; performance
     (icon :category "test" :icon "cog") ;; add/correct tests
     (icon :category "build" :icon "cogs") ;; build tooling
     (icon :category "chore" :icon "coffee") ;; other nor src || test
     (icon :category "reverts" :icon "backward")))) ;; revert/rebase commit

(customize-set-value
    'org-agenda-category-icon-alist
    (cl-flet ((icons (lambda (x) (all-the-icons-faicon (agendaIcons-icon x) :height 1))))
      (mapcar
       (lambda (x) (list (agendaIcons-category x)
                    (list (icons x))
                    nil nil)) agenda-icons/caticons)))
#+end_src

** Org habit
#+begin_src emacs-lisp
(after! org
  (add-to-list 'org-modules 'org-habit))
#+end_src
** Org Refile
#+begin_src emacs-lisp
;; Org Refile Targets
(after! org-refile
  (setq org-refile-targets
        '((nil :maxlevel . 1) ; Same buffer
          (org-agenda-files :maxlevel . 1)
          (org-buffer-list :maxlevel . 1)
          (_archiveorg :maxlevel . 1)
          (_stageorg :maxlevel . 1))))
#+end_src
** Org Capture
I capture every activity on my laptop by (broad) category. May change over time. Currently, there are following activities:
- development - concrete project development
- research - various technology related explorative/design work
- operations - linux/emacs related time sinks
- spanish - language learning
- cooking - offline topic; Gathering of cooking recipes
#+begin_src emacs-lisp
;; Org Capture Templates
(after! org-capture
  (setq org-capture-templates
         '(("p" "Blog Post" entry (file+headline "~/data/git/ja.nz/README.org" "Posts") "* TODO %^{title}\nSCHEDULED: %t%^{export_hugo_bundle}p%^{export_file_name}p\n#+begin_description\n%?\n#+end_description\n** scratchpad :noexport:\n" :prepend t :jump-to-captured t)
           ("x" "Instant Todo" entry (function org-journal-open-current-journal-file) "* TODO %^{title}\nSCHEDULED: %T%^{CATEGORY}p%^{Effort}p\n%?" :jump-to-captured t))))
#+end_src
*** Backup (untangled)
Backup of the Doom Emacs Capture templates for future reference
#+begin_src
;;https://github.com/hlissner/doom-emacs/blob/f621ff80471e8d08a72e5ece00641c70b121873a/modules/lang/org/config.el#L342
(("t" "Personal todo" entry
  (file+headline +org-capture-todo-file "Inbox")
  "* [ ] %?\n%i\n%a" :prepend t)
 ("n" "Personal notes" entry
  (file+headline +org-capture-notes-file "Inbox")
  "* %u %?\n%i\n%a" :prepend t)
 ("j" "Journal" entry
  (file+olp+datetree +org-capture-journal-file)
  "* %U %?\n%i\n%a" :prepend t)
 ("p" "Templates for projects")
 ("pt" "Project-local todo" entry
  (file+headline +org-capture-project-todo-file "Inbox")
  "* TODO %?\n%i\n%a" :prepend t)
 ("pn" "Project-local notes" entry
  (file+headline +org-capture-project-notes-file "Inbox")
  "* %U %?\n%i\n%a" :prepend t)
 ("pc" "Project-local changelog" entry
  (file+headline +org-capture-project-changelog-file "Unreleased")
  "* %U %?\n%i\n%a" :prepend t)
 ("o" "Centralized templates for projects")
 ("ot" "Project todo" entry #'+org-capture-central-project-todo-file "* TODO %?\n %i\n %a" :heading "Tasks" :prepend nil)
 ("on" "Project notes" entry #'+org-capture-central-project-notes-file "* %U %?\n %i\n %a" :heading "Notes" :prepend t)
 ("oc" "Project changelog" entry #'+org-capture-central-project-changelog-file "* %U %?\n %i\n %a" :heading "Changelog" :prepend t))
#+end_src
** Org Journal -> SPC r
#+begin_src emacs-lisp
;; Org Journal Settings
(setq org-journal-dir org-directory
      org-journal-date-prefix "#+title: "
      org-journal-date-format "W%V_%Y-%m-%d"
      org-journal-time-prefix "* "
      org-journal-file-format "W%V_%Y-%m-%d.org"
      org-journal-file-header "#+STARTUP: overview\n"
      ;; But #+title tag back to first line
      org-journal-after-header-create-hook (lambda () (transpose-lines 1))
      ;; Automatic org agenda integration
      org-journal-after-entry-create-hook
      (lambda () (if (not (file-exists-p (buffer-file-name))) (org-agenda-file-to-front t))))
;;org-journal-file-header "#+title: W%V_%Y-%m-%d\n#+roam_key: file:_stage.org\n"
;;org-journal-skip-carryover-drawers (list "LOGBOOK")
#+end_src

*** Org Journal Keymap -> SPC r
#+begin_src emacs-lisp
(after! org-journal
  (map! :map org-journal-mode-map "C-M-n" #'org-journal-previous-entry)
  (map! :map org-journal-mode-map "C-M-t" #'org-journal-next-entry)
  ;; I want org-mode bindings instead of org-journal bindings
  ;; Overwriting SPC c, d, p
  (map! :localleader
        (:map org-journal-mode-map
         (:prefix ("c" . "clock")
          "c" #'org-clock-cancel
          "d" #'org-clock-mark-default-task
          "e" #'org-clock-modify-effort-estimate
          "E" #'org-set-effort
          "g" #'org-clock-goto
          "G" (cmd! (org-clock-goto 'select))
          "l" #'+org/toggle-last-clock
          "i" #'org-clock-in
          "I" #'org-clock-in-last
          "o" #'org-clock-out
          "r" #'org-resolve-clocks
          "R" #'org-clock-report
          "t" #'org-evaluate-time-range
          "=" #'org-clock-timestamps-up
          "-" #'org-clock-timestamps-down)
         (:prefix ("d" . "date/deadline")
          "d" #'org-deadline
          "s" #'org-schedule
          "t" #'org-time-stamp
          "T" #'org-time-stamp-inactive)
         (:prefix ("p" . "priority")
          "d" #'org-priority-down
          "p" #'org-priority
          "u" #'org-priority-up)
          "n" #'org-store-link)))
(map! :leader :prefix "r"
        (:map org-mode-map
         :desc "Org Agenda File To Front" "f" #'org-agenda-file-to-front)
         :desc "Org Journal Previous" "n" #'org-journal-previous-entry
         :desc "Org Journal Next" "t" #'org-journal-next-entry
        (:map global-map
         :desc "Org Journal New Entry" "s" #'org-journal-new-entry
         :desc "Org Journal New Scheduled Entry" "S" #'org-journal-new-scheduled-entry
         :desc "Org Journal Open Current" "r" #'org-journal-open-current-journal-file
         :desc "Org Journal Stage" "h" #'(lambda () (interactive) (find-file _stageorg))))
#+end_src
*** Custom Export Clocktable Function :deprecated:
#+begin_src
(defun export-clocktable-csv (&optional week)
  "Export current week (no prefix argument) or weeks in the "
  (interactive "P")
  (let* ((week (if week week 0))
         (time-string (format-time-string "%V"))
         (new-time-number (- (string-to-number time-string) week))
         (new-time-string (number-to-string new-time-number))
         (time-string (if (< new-time-number 10)
                          (concat "0" new-time-string) new-time-string))
         (org-agenda-files (directory-files org-directory t (concat "^W" time-string))))
    (call-interactively #'org-clock-csv-to-file)))
#+end_src
** Org Roam -> SPC r
#+begin_src emacs-lisp
;; Org Roam Settings
(setq +org-roam-open-buffer-on-find-file nil
      org-roam-directory org-directory
      org-roam-capture-templates
      '(("d" "default" plain
         "%?"
         :if-new
         (file+head "%<%Y%m%d%H%M%S>-${slug}.org"
                    "#+title: ${title}\n#+CREATED: %(org-insert-time-stamp (org-read-date nil t \"+0d\"))\n#+REVISION: %(org-insert-time-stamp (org-read-date nil t \"+0d\"))\n#+STARTUP: overview\n")
         :unnarrowed t)))

(after! org-roam
  (setq org-roam-completion-everywhere nil))

(defun org_roam__bump_revision_date ()
  "Retriving REVISION and replace it naively with current time stamp."
  (when (string-match-p "^[0-9]\\{14\\}-" (file-name-base))
    (let ((lastrev (car (cdr (car (org-collect-keywords '("REVISION"))))))
          (today (format-time-string (org-time-stamp-format))))
      (cond ((not lastrev) nil)
            ((not (string= lastrev today))
             (progn (push-mark)
                    (re-search-backward "REVISION" nil 1)
                    (if (re-search-forward lastrev nil 1)
                        (replace-match today))
                    (pop-global-mark)))))))

(add-hook! org-mode
  (add-hook 'after-save-hook #'org_roam__bump_revision_date))

;; (use-package! org-roam
;;   :bind (:map org-mode-map
;;          ("M-s-s i" . org-roam-node-insert) ;; insert links in org documents
;;          ("M-s-s b" . org-roam-buffer-toggle) ;; toggle backlinks overview
;;          :map global-map
;;          ("M-s-s f" . org-roam-node-find) ;; quickly find
;;          ("M-s-s c" . org-roam-capture))) ;; capture information

(map! :leader :prefix "r"
        (:map org-mode-map
         :desc "Org Roam Node Insert" "i" #'org-roam-node-insert
         ;;:desc "Org Table Column Toggle" "t" #'org-table-toggle-column-width)
         :desc "Org Roam Buffer Toggle" "b" #'org-roam-buffer-toggle)
        (:map global-map
         :desc "Org Roam Goto Node" "g" #'org-roam-node-find
         :desc "Org Roam Capture" "c" #'org-roam-capture))
#+end_src
** Org MRU Clock
#+begin_src emacs-lisp :tangle ~/.doom.d/packages.el
(package! org-mru-clock)
#+end_src

#+begin_src emacs-lisp
(defun org/insert-clock-entry ()
  (interactive)
  (insert "CLOCK: ")
  (org-time-stamp-inactive)
  (insert "--")
  ;; Inserts the current time by default.
  (let ((current-prefix-arg '(4))) (call-interactively 'org-time-stamp-inactive))
  (org-ctrl-c-ctrl-c))

(map! :map org-mode-map :localleader :prefix "c"
      :desc "Org MRU clock" "m" #'org-mru-clock-in
      :desc "Append manual clock entry" "a" #'org/insert-clock-entry
      :desc "Org Update All DBlocks" "u" #'org-update-all-dblocks)

;; (use-package! org-mru-clock
;;   :bind (:map global-map
;;          ("M-s-t r" . org-mru-clock-in)
;;          ("M-s-t i" . org-clock-in)
;;          ("M-s-t o" . org-clock-out)
;;          ("M-s-t u" . org-update-all-dblocks)))
#+end_src
** Org clock export to csv
#+begin_src emacs-lisp :tangle ~/.doom.d/packages.el
(package! org-clock-csv)
#+end_src
** Org pomodoro
#+begin_src emacs-lisp
(after! org-pomodoro
  (setq org-pomodoro-audio-player nil))
  ;; (setq org-pomodoro-audio-player (executable-find "notify-send")))

(setq org-pomodoro-start-sound-p t
      org-pomodoro-killed-sound-p t
      org-pomodoro-start-sound "GOâ°"
      org-pomodoro-finished-sound "FINISHðŸ"
      org-pomodoro-overtime-sound "OVERTIMEðŸ˜µ"
      org-pomodoro-killed-sound "KILLEDðŸ’€"
      org-pomodoro-short-break-sound "SHORTBREAKâ˜• FINISHEDðŸ"
      org-pomodoro-long-break-sound "LONGBREAKðŸ˜´ FINISHEDðŸ")
#+end_src
** Org hunspell
#+begin_src emacs-lisp
(add-hook! org-mode
  (after! ispell
    (ispell-set-spellchecker-params)
    (ispell-hunspell-add-multi-dic "it_IT,en_US")
    (ispell-hunspell-add-multi-dic "tr_TR,en_US")
    ;;(ispell-hunspell-add-multi-dic "pt_BR,en_US")
    ;;(ispell-hunspell-add-multi-dic "fr_FR,en_US")
    ;;(ispell-hunspell-add-multi-dic "es_ES,en_US")
))
#+end_src
** Org table copy cell :hack:
#+begin_src emacs-lisp
(defun org/org-table-yank-cell ()
  "Copy cell value and trim surrounding whitepaces."
  (interactive)
  (when (org-at-table-p)
    (kill-new
      (string-trim
        (substring-no-properties(org-table-get-field))))))
#+end_src
** Org purge drawers :hack:
If the habits.org file gets too big it slows down emacs.
This function will clean all the logbook entries.
#+begin_src emacs-lisp
(defun org/purge-logbook-drawer ()
  (interactive)
  (goto-line 1)
  (delete-matching-lines ":LOGBOOK:\\(\n\\|.\\)*?:END:")
  (pop-global-mark))
#+end_src

** Org babel execute graph-easy :hack:
#+begin_example
\#+BEGIN_SRC graph-easy
strict digraph {
    a [shape="ellipse" style="filled" fillcolor="#1f77b4"]
    b [shape="polygon" style="filled" fillcolor="#ff7f0e"]
    a -> b [fillcolor="#a6cee3" color="#1f78b4"]
}
\#+END_SRC
#+end_example
- cmdline
  - according to easy-graph. Default: --as=ascii
  - useful: --as=boxart|svg|graphml
- file
  - outfile. Default: /dev/stdout
  - outputformat is inferred from file extension (if not overwritten manually with --as=_)

#+begin_src emacs-lisp
(defun org-babel-execute:graph-easy (body params)
  "Execute a block of dot code with org-babel:graph-easy."
  (let* ((out-file (or (cdr (assq :file params)) "/dev/stdout"))
        (in-file (org-babel-temp-file "tmp" ".dot"))
        (cmdline (or (cdr (assq :cmdline params))
                     (if-let ((ext (file-name-extension out-file)))
                         (format "--as=%s" ext)
                         (format "--as=%s" "ascii"))))
        (cmd (or (cdr (assq :cmd params)) "graph-easy")))
    (with-temp-file in-file
      (insert body))
    (org-babel-eval
     (concat cmd
        " --input=" (org-babel-process-file-name in-file)
        " " cmdline
        " --output=" (org-babel-process-file-name out-file)) "")))
#+end_src
* MU4E
[[ https://github.com/hlissner/doom-emacs/blob/develop/modules/email/mu4e/README.org][Doom Emacs MU4E]]
#+begin_src emacs-lisp
;; Mu4e settings
(add-to-list 'load-path "~/.nix-profile/share/emacs/site-lisp/mu4e")
(after! mu4e
  (setq mu4e-update-interval 300
        mu4e-sent-messages-behavior (lambda () (if (string-suffix-p "gmail.com" (message-sendmail-envelope-from)) 'delete 'sent))))

(setq mu4e-get-mail-command "mbsync -a"
      starttls-use-gnutls t
      message-citation-line-format "On %a, %d %b %Y at %R, %f wrote:\n"
      message-citation-line-function 'message-insert-formatted-citation-line)
#+end_src

** mail@ja.nz :deprecated:
Handled by gmail now
#+begin_src
(set-email-account! "mail@ja.nz"
                    '((user-mail-address . "mail@ja.nz")
                      (user-full-name . "Ja0nz")
                      (mu4e-sent-folder . "/mail@ja.nz/Sent")
                      (mu4e-drafts-folder . "/mail@ja.nz/Drafts")
                      (mu4e-trash-folder . "/mail@ja.nz/Trash")
                      (mu4e-refile-folder . "/mail@ja.nz/Archive")
                      (mu4e-compose-signature . "\nðŸ¤– Jan Peteler\nðŸ’Œ mail@ja.nz\nðŸ”– ja.nz")
                      (smtpmail-smtp-server . "smtp.purelymail.com")
                      (smtpmail-smtp-service . 587)
                      (smtpmail-smtp-user . "mail@ja.nz")
                      (smtpmail-stream-type . starttls)))
#+end_src
** jan.peteler@gmail.com
#+begin_src emacs-lisp
(set-email-account! "jan.peteler@gmail.com"
                    '((user-mail-address . "jan.peteler@gmail.com")
                      (user-full-name . "Jan")
                      (mu4e-sent-folder . "/jan.peteler@gmail.com/Sent")
                      (mu4e-drafts-folder . "/jan.peteler@gmail.com/Drafts")
                      (mu4e-trash-folder . "/jan.peteler@gmail.com/Trash")
                      (mu4e-refile-folder . "/jan.peteler@gmail.com/Archive")
                      (mu4e-compose-signature . "\nðŸ¤– Jan Peteler\nðŸ’Œ jan.peteler@gmail.com\nðŸ”– ja.nz")
                      (smtpmail-smtp-server . "smtp.gmail.com")
                      (smtpmail-smtp-service . 587)
                      (smtpmail-smtp-user . "jan.peteler@gmail.com")
                      (smtpmail-stream-type . starttls)))
#+end_src
** Org Contacts Action
#+begin_src emacs-lisp
(setq mu4e-org-contacts-file "~/org/contacts.org")
(after! mu4e
  (add-to-list 'mu4e-headers-actions '("org-contact-add" . mu4e-action-add-org-contact) t)
  (add-to-list 'mu4e-view-actions '("org-contact-add" . mu4e-action-add-org-contact) t))
#+end_src
* Calendar
#+begin_src emacs-lisp
(setq calendar-date-style "european"
      calendar-week-start-day 1)
#+end_src
* Terminal Here
#+begin_src emacs-lisp :tangle ~/.doom.d/packages.el
(package! terminal-here)
#+end_src

#+begin_src emacs-lisp
(setq terminal-here-linux-terminal-command '("alacritty"))
(use-package! terminal-here
  :init
  (map! :leader
        :prefix "o"
        :desc "Launch terminal here" "t" #'terminal-here-launch
        :desc "Launch terminal ROOT" "T" #'terminal-here-project-launch))
#+end_src
* Avy Actions
Based on the article:
https://karthinks.com/software/avy-can-do-anything/

Pomodoro clock in -> t
#+begin_src emacs-lisp
(defun avy-action-org-pomodoro (pt)
  (save-excursion
    (goto-char pt)
    (org-pomodoro))
  (select-window
   (cdr
    (ring-ref avy-ring 0)))
  t)

(after! avy
  (setf (alist-get ?t avy-dispatch-alist) 'avy-action-org-pomodoro))
#+end_src

Kill text -> k K
#+begin_src emacs-lisp
;; Kill text
(defun avy-action-kill-whole-line (pt)
  (save-excursion
    (goto-char pt)
    (kill-whole-line))
  (select-window
   (cdr
    (ring-ref avy-ring 0)))
  t)

(after! avy
  (setf (alist-get ?k avy-dispatch-alist) 'avy-action-kill-stay
        (alist-get ?K avy-dispatch-alist) 'avy-action-kill-whole-line))
#+end_src

Copy text (remain in register) -> c C
Yank text (paste immediately) -> y Y | ALTERSBUFFER
#+begin_src emacs-lisp
;; Copy text (remain in register)
(defun avy-action-copy-whole-line (pt)
  (save-excursion
    (goto-char pt)
    (cl-destructuring-bind (start . end)
        (bounds-of-thing-at-point 'line)
      (copy-region-as-kill start end)))
  (select-window
   (cdr
    (ring-ref avy-ring 0)))
  t)

(after! avy
  (setf (alist-get ?c avy-dispatch-alist) 'avy-action-copy
        (alist-get ?C avy-dispatch-alist) 'avy-action-copy-whole-line))

;; Yank text (paste immediately)
(defun avy-action-yank-whole-line (pt)
  (avy-action-copy-whole-line pt)
  (save-excursion (yank))
  t)

(after! avy
  (setf (alist-get ?y avy-dispatch-alist) 'avy-action-yank
        (alist-get ?Y avy-dispatch-alist) 'avy-action-yank-whole-line))
#+end_src

Teleport/Grab -> g G
#+begin_src emacs-lisp
;; Transpose/Move text
(defun avy-action-teleport-whole-line (pt)
  (avy-action-kill-whole-line pt)
  (save-excursion (yank)) t)

(after! avy
  (setf (alist-get ?g avy-dispatch-alist) 'avy-action-teleport
        (alist-get ?G avy-dispatch-alist) 'avy-action-teleport-whole-line))
 #+end_src

_ Mark to char -> M
#+begin_src emacs-lisp
(defun avy-action-mark-to-char (pt)
  (activate-mark)
  (goto-char pt))

(after! avy
  (setf (alist-get ?M  avy-dispatch-alist) 'avy-action-mark-to-char))
#+end_src

Flyspell -> ;
#+begin_src emacs-lisp
;; Flyspell words
(defun avy-action-flyspell (pt)
  (save-excursion
    (goto-char pt)
    (when (require 'flyspell nil t)
      (flyspell-auto-correct-word)))
  (select-window
   (cdr (ring-ref avy-ring 0)))
  t)

;; Bind to semicolon (flyspell uses C-;)
(after! avy
  (setf (alist-get ?\; avy-dispatch-alist) 'avy-action-flyspell))
#+end_src

Embark -> .
#+begin_src emacs-lisp
(defun avy-action-embark (pt)
  (unwind-protect
      (save-excursion
        (goto-char pt)
        (embark-act))
    (select-window
     (cdr (ring-ref avy-ring 0))))
  t)
(after! avy
  (setf (alist-get ?. avy-dispatch-alist) 'avy-action-embark))
#+end_src

* Envrc global mode
#+begin_src emacs-lisp
(envrc-global-mode)
#+end_src

* Racket mode
#+begin_src emacs-lisp
;;(add-to-list '+format-on-save-enabled-modes 'racket-mode t)
#+end_src

* JS/TS mode
#+begin_src emacs-lisp
;;https://github.com/js-emacs/xref-js2
(setq xref-js2-search-program 'rg)
(setq js-indent-level 2)
#+end_src

* Artist mode
#+begin_src emacs-lisp
(add-hook! artist-mode
  (evil-emacs-state 1))
#+end_src
** Ascii-art-to-unicode
#+begin_src emacs-lisp :tangle ~/.doom.d/packages.el
(package! ascii-art-to-unicode)
#+end_src
* Web Mode
** Config
#+begin_src emacs-lisp
(setq web-mode-markup-indent-offset 2)
(setq-hook! 'web-mode-hook +format-with :none)
#+end_src

** Web Mode :hack:
Related to this issue of web mode: https://github.com/fxbois/web-mode/issues/799
In short: changing dir-locals alone is not working with web mode. This is a hack
to get it working.

#+begin_src emacs-lisp
(add-to-list 'safe-local-variable-values '(hack-web-mode-engine . "go"))
(defun hack-web-mode-hook ()
  "Hooks for Web mode. Add a local hook which set the engine to the one specified by
   `hack-web-mode-engine` local variable."
  (add-hook
   'hack-local-variables-hook
   (lambda ()
     (if (boundp 'hack-web-mode-engine)
         (progn
           (message "web-mode-engine is %s" hack-web-mode-engine)
           (web-mode-set-engine hack-web-mode-engine))
       (progn
         (message "no web-mode-engine settled")
         (web-mode-set-engine "none"))))))
(add-hook 'web-mode-hook  'hack-web-mode-hook nil nil)
#+end_src
* Tailwind CSS
#+begin_src emacs-lisp :tangle ~/.doom.d/packages.el
;;(package! lsp-tailwindcss :recipe (:host github :repo "merrickluo/lsp-tailwindcss"))
#+end_src

#+begin_src emacs-lisp
;;(use-package! lsp-tailwindcss)
#+end_src
* Undo tree keybindings -> SPC d
https://elpa.gnu.org/packages/undo-tree.html

Missing argument: undo-tree-switch-branch
I could not find any use from it because going over undo-tree-visualize anyway

#+begin_src emacs-lisp
(map! :leader :prefix "d"
        (:map global-map
         :desc "better undo-tree-visualize" "d" #'undo-tree-visualize
         :desc "save buffer state to register" "s" #'undo-tree-save-state-to-register
         :desc "restore buffer state from register" "r" #'undo-tree-restore-state-from-register))

(map! :nie "C-r" #'undo-tree-undo)
(map! :nie "C-M-r" #'undo-tree-redo)
#+end_src
